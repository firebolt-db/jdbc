import java.util.jar.JarFile

plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "6.1.0.5360"
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'signing'
}

group 'io.firebolt'

repositories {
    mavenCentral()
}

project.ext {
    codeCoverageExclusionList = [
            "**/firebolt/jdbc/client/config/**"
    ]
}

java {
    withSourcesJar()
    withJavadocJar()
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompileOnly.extendsFrom compileOnly
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

// Generate file containing project version
def generatedVersionDir = "${buildDir}/resources"

sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }

    testCommon {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

}
dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.json:json:20250107'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'net.jodah:expiringmap:0.5.11'
    implementation 'org.apache.commons:commons-text:1.13.1'
    implementation 'org.lz4:lz4-java:1.8.0'
    implementation 'commons-validator:commons-validator:1.8.0'

    implementation fileTree(dir: 'libs', includes: ['*.jar'])

    compileOnly 'org.slf4j:slf4j-api:2.0.17'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'

    testImplementation 'ch.qos.logback:logback-classic:1.5.18'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.12.2'
    testImplementation 'org.junit-pioneer:junit-pioneer:2.2.0'
    testImplementation 'org.hamcrest:hamcrest-library:3.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'com.squareup.okhttp3:okhttp-tls:4.12.0'
    testImplementation 'io.zonky.test:embedded-postgres:2.1.0'
    testCompileOnly 'org.slf4j:slf4j-api:2.0.17'
    testCommonImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.2'
    testCommonImplementation 'org.junit.jupiter:junit-jupiter-params:5.12.2'
    testImplementation sourceSets.testCommon.output
    compileTestJava.dependsOn processTestResources
    jar.dependsOn processTestResources
    jar.dependsOn processIntegrationTestResources
    javadoc.dependsOn processTestResources
    javadoc.dependsOn processIntegrationTestResources
    compileTestCommonJava.dependsOn processTestResources
    compileTestCommonJava.dependsOn processIntegrationTestResources
    compileIntegrationTestJava.dependsOn processIntegrationTestResources
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}

test {
    jvmArgs(
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
            "--add-opens=java.base/java.util=ALL-UNNAMED"
    ) // so we can change the environment variables
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        exceptionFormat = 'full'
        showStackTraces = true
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    useJUnitPlatform() {
        includeTags(System.getProperty("includeTags", "common").split(","))
        excludeTags(System.getProperty("excludeTags", "nothing").split(","))
    }
    reports {
        junitXml {
            outputPerTestCase = true // defaults to false
            mergeReruns = true // defaults to false
        }
    }
    options {
        systemProperties(System.getProperties())
    }
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    // Temporarily disabled multithreaded execution due to bug on server side.
    //maxParallelForks = (int) (Runtime.runtime.availableProcessors() / 2 + 1)

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

allprojects {
    // add a collection to track failedTests
    ext.failedTests = []

    // add a testlistener to all tasks of type Test
    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE){
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    // print out tracked failed tests when the build has finished
    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed tests for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
            println "End of the list"
        } else {
            println "No failures found"
        }
    }
}

jacoco {
    toolVersion = "0.8.10"
}


jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: project.codeCoverageExclusionList)
        }))
    }
    reports {
        xml.required = true
    }
}

shadowJar {
    // rename the shadow jar to the original jar by omitting the classifier
    archiveClassifier.set('')
    // move all packages except org.slf4j for now
    def packages = [] as Set<String>
    configurations.each { configuration ->
        configuration.files.each { jar ->
            JarFile jf = new JarFile(jar)
            jf.entries().each { entry ->
                if (entry.name.endsWith(".class") && entry.name != "module-info.class" &&
                    !entry.name.startsWith('org/slf4j') && !entry.name.startsWith('ch/qos/logback')) {
                    packages << entry.name[0..entry.name.lastIndexOf('/')-1].replaceAll('/', '.')
                }
            }
            jf.close()
        }
    }
    packages.each {
        relocate(it, "com.firebolt.shadow.${it}")
    }
    exclude("test/**")
    exclude("integrationTest/**")
}

tasks.assemble.dependsOn shadowJar

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties

task printVersion {
    println "PROJECT_VERSION=${project.version}"
}
sonarqube {
    properties {
        property "sonar.java.source", "11"
        property "sonar.projectKey", "firebolt-db_jdbc"
        property "sonar.organization", "firebolt-db"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.coverage.exclusions', project.codeCoverageExclusionList
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "firebolt-jdbc"
                description = 'Type 4 JDBC driver that allows connection to Firebolt Database'
                url = "https://github.com/firebolt-db/jdbc"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "JonyBoyko"
                        name = "Ivan Boyko"
                        email = "107884978+yingli017@users.noreply.github.com"
                    }
                    developer {
                        id = "ptiurin"
                        name = "Petro Tiurin"
                        email = "petro.tiurin@firebolt.io"
                    }
                    developer {
                        id = "kevin"
                        name = "Kevin Marr"
                        email = "kevin@firebolt.io"
                    }
                    developer {
                        id = "aymeric"
                        name = "Aymeric Dispa"
                        email = "aymeric@firebolt.io"
                    }
                    developer {
                        id = "alexradzin"
                        name = "Alexander Radzin"
                        email = "alexander.radzin@firebolt.io"
                    }
                    developer {
                        id = "reelsen"
                        name = "Alexander Reelsen"
                        email = "alexander@reelsen.net"
                    }
                }
                scm {
                    connection = "scm:svn:https://github.com/firebolt-db/jdbc/"
                    developerConnection = "scm:svn:https://github.com/firebolt-db/jdbc/"
                    url = "https://github.com/firebolt-db/jdbc/"
                }
            }
            // remove dependencies due to shaded jar
            pom.withXml {
                node = asNode()
                node.remove(node.get('dependencies'))
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("MAVEN_REPO_USERNAME")
                password System.getenv("MAVEN_REPO_PASSWORD")
            }
        }
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Firebolt JDBC driver', // This value must be equal to one defined in VersionUtil
                'Implementation-Vendor': 'Firebolt Analytics',
                'Implementation-Version': version,
                'Specification-Title': 'JDBC',
                'Specification-Version': jdbcVersion,
                'Specification-Vendor': 'Oracle Corporation',
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}


task generateJavadoc(type: Javadoc) {
    failOnError false
    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath
    classpath = classpath + sourceSets.main.compileClasspath
    options {
        links = ['https://docs.oracle.com/javase/8/docs/api/']
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
    def signingKey = System.getenv("GRADLE_SIGNING_KEY")
    def signingPassword = System.getenv("GRADLE_SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
