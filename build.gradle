plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.4.0.2513"
    id 'maven-publish'
}

group 'com.firebolt'
version = '2.0.1'

repositories {
    mavenCentral()
}

project.ext {
    codeCoverageExclusionList = [
            "**/firebolt/jdbc/client/config/**",
            "**/firebolt/jdbc/**/Abstract*.*",
    ]
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompileOnly.extendsFrom compileOnly
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

// Generate file containing project version
def generatedVersionDir = "${buildDir}/resources"

sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }

    testCommon {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

}
dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'net.jodah:expiringmap:0.5.10'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
    implementation 'org.apache.httpcomponents.core5:httpcore5:5.1.4'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.lz4:lz4-java:1.8.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'ch.qos.logback:logback-classic:1.2.11'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.7.0'
    testImplementation 'org.mockito:mockito-core:4.7.0'
    testImplementation 'org.mockito:mockito-inline:4.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation group: 'org.junit-pioneer', name: 'junit-pioneer', version: '1.7.1'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testCommonImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation sourceSets.testCommon.output
    compileTestJava.dependsOn processTestResources
    jar.dependsOn processTestResources
    compileTestCommonJava.dependsOn processTestResources
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    useJUnitPlatform()
    options {
        systemProperties(System.getProperties())
    }
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    maxParallelForks = (int) (Runtime.runtime.availableProcessors() / 2 + 1)
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: project.codeCoverageExclusionList)
        }))
    }
    reports {
        xml.required = true
    }
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties

task printVersion {
    println project.version
}
sonarqube {
    properties {
        property "sonar.projectKey", "firebolt-db_jdbc"
        property "sonar.organization", "firebolt-db"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.coverage.exclusions', project.codeCoverageExclusionList
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://repo.repsy.io/mvn/firebolt/maven'
            def snapshotsRepoUrl = 'https://repo.repsy.io/mvn/firebolt/maven-snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("REPSY_USERNAME")
                password System.getenv("REPSY_PASSWORD")
            }
        }
    }
}

task generateJavadoc(type: Javadoc) {
    failOnError false
    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath
    classpath = classpath + sourceSets.main.compileClasspath
    options {
        links = ['https://docs.oracle.com/javase/8/docs/api/']
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}