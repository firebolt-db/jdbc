plugins {
    id 'java'
    id 'jacoco'
}

group 'io.firebolt'
version '2.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'net.jodah:expiringmap:0.5.10'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.5.1'
    testImplementation 'org.mockito:mockito-core:4.5.1'
    testImplementation 'org.mockito:mockito-inline:4.5.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "io/firebolt/jdbc/client/config/**",
                    "io/firebolt/jdbc/FireboltDriver.class",
                    "io/firebolt/jdbc/connection/settings/FireboltProperties.class",
                    "io/firebolt/jdbc/resultset/array/AbstractArray.class",
                    "io/firebolt/jdbc/resultset/AbstractResultSet.class",
                    "io/firebolt/jdbc/resultset/array/AbstractArray.class",
                    "io/firebolt/jdbc/statement/AbstractStatement.class",
                    "io/firebolt/jdbc/connection/databasemetadata/FireboltDatabaseMetadata.class",
                    "io/firebolt/jdbc/connection/FireboltDatabaseMetadata.class",
                    "io/firebolt/jdbc/connection/AbstractConnection.class",
            ])
        }))
    }
    reports {
        csv.enabled true
    }
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// Generate file containing project version
def generatedVersionDir = "${buildDir}/generated-version"

sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties

task printVersion {
    println project.version
}